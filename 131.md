Glorious Walnut Armadillo

Medium

# `forwardAll()` must be called before `cancelStream()` to ensure accurate refund calculations and maintain fairness among users.

### Summary

The `forwardAll()` function should be called before `cancelStream()`. However, the code fails to ensure this sequence. As a result, if the tick time has passed but `forwardAll()` has not been called, the `currentTick` remains stale. Consequently, when `cancelStream()` is executed, it calculates the refund based on the outdated `currentTick`, leading to the user receiving a higher refund than expected.

### Root Cause



The `cancelStream()` function is called by a noun owner who has an active stream in `StreamEscrow.sol`.  

https://github.com/sherlock-audit/2024-11-nounsdao/blob/main/nouns-monorepo/packages/nouns-contracts/contracts/StreamEscrow.sol#L167-L186

```solidity
function cancelStream(uint256 nounId) public {
    require(isStreamActive(nounId), 'stream not active');

    // transfer noun to treasury
    nounsToken.transferFrom(msg.sender, nounsRecipient, nounId);

    // cancel stream
    streams[nounId].canceled = true;
    Stream memory stream = streams[nounId];
    ethStreamedPerTick -= stream.ethPerTick;
    ethStreamEndingAtTick[stream.lastTick] -= stream.ethPerTick;

    // calculate how much needs to be refunded
    uint256 ticksLeft = stream.lastTick - currentTick;
    uint256 amountToRefund = stream.ethPerTick * ticksLeft;
    (bool sent, ) = msg.sender.call{ value: amountToRefund }('');
    require(sent, 'failed to send eth');

    emit StreamCanceled(nounId, amountToRefund, ethStreamedPerTick);
}
```

This function calculates the unstreamed amount based on the ticks left and refunds the owner by transferring the Noun NFT to the treasury.  

```solidity
    // calculate how much needs to be refunded
    uint256 ticksLeft = stream.lastTick - currentTick;
    uint256 amountToRefund = stream.ethPerTick * ticksLeft;
    (bool sent, ) = msg.sender.call{ value: amountToRefund }('');
    require(sent, 'failed to send eth');
```

From the above snippet, we can see that the calculation of `amountToRefund` is based on `currentTick`.  

Now, let’s examine `forwardAll()`:  

https://github.com/sherlock-audit/2024-11-nounsdao/blob/main/nouns-monorepo/packages/nouns-contracts/contracts/StreamEscrow.sol#L136

```solidity
    if (block.timestamp < lastForwardTimestamp + minimumTickDuration) {
        return;
    }
```

The `currentTick` is updated only when 24 hours (the `minimumTickDuration`) have passed since the `lastForwardTimestamp`. The `forwardAll()` function is expected to be called by the Auction House contract through `forwardAllAndCreateStream()` when settling an auction.  

Since auctions in the Auction House generally take longer than 24 hours, and there is no incentive for other users to call `forwardAll()`, the `currentTick` often remains outdated. Consequently, if a user cancels their stream, the refund amount is calculated based on the stale `currentTick`, leading to an inaccurate refund.  



### Internal pre-conditions

1. Auction should not be settled
2. block.timestamp > lastForwardTimestamp + minimumTickDuration
3. forwardAll() should not be called  by any user.


### External pre-conditions

_No response_

### Attack Path



Let’s imagine Alice has an active stream with the following parameters:  

- `ethPerTick = 10 wei`  
- `lastTick = 11`  
- `canceled = false`  

Now, suppose the `currentTick` is 5 (meaning 40 wei have already been streamed).  

Additionally, assume that `block.timestamp > lastForwardTimestamp + minimumTickDuration`. However, since the ongoing auction has not been settled and no one has called `forwardAll()`, the `currentTick` remains at 5, even though it should have been updated to 6.  

The refund is then calculated as follows:  
```plaintext
ticksLeft = lastTick - currentTick = 11 - 5 = 6  
refund = ticksLeft * ethPerTick = 6 * 10 = 60 wei  
```

In reality, the `currentTick` should be at 6, and the refund should have been:  
```plaintext
ticksLeft = lastTick - currentTick = 11 - 6 = 5  
refund = ticksLeft * ethPerTick = 5 * 10 = 50 wei  
```

As a result of this issue, Alice receives **60 wei** instead of the correct amount, which is **50 wei**.  



### Impact


The impact of this issue is that users receive a higher refund than they should, which creates an unfair situation among users.  

For instance, imagine there are two users:  
- The first user cancels their stream **before** `forwardAll()` is called.  
- The second user cancels their stream **after** `forwardAll()` is called.  

In this scenario, the first user would receive a higher refund because their refund calculation is based on an outdated `currentTick`, whereas the second user’s refund is calculated using the updated `currentTick`.  

This inconsistency highlights the lack of a guarantee that `forwardAll()` will be called before a stream is canceled. To address this issue, `forwardAll()` should be invoked within the `cancelStream()` function to ensure fairness and accurate refund calculations for all users.  



### PoC

_No response_

### Mitigation

```solidity

    function cancelStream(uint256 nounId) public {
        
        forwardAll();  //----------------------> Add this
        require(isStreamActive(nounId), 'stream not active');


        // transfer noun to treasury
        nounsToken.transferFrom(msg.sender, nounsRecipient, nounId);


        // cancel stream
        streams[nounId].canceled = true;
        Stream memory stream = streams[nounId];
        ethStreamedPerTick -= stream.ethPerTick;
        ethStreamEndingAtTick[stream.lastTick] -= stream.ethPerTick;


        // calculate how much needs to be refunded
        uint256 ticksLeft = stream.lastTick - currentTick;
        uint256 amountToRefund = stream.ethPerTick * ticksLeft;
        (bool sent, ) = msg.sender.call{ value: amountToRefund }('');
        require(sent, 'failed to send eth');


        emit StreamCanceled(nounId, amountToRefund, ethStreamedPerTick);
    }

```