Clean Indigo Stallion

Medium

# `Auction Settlement` DDOS because `streamEscrow::sendETHToTreasury` Fails to Handle `Non-ETH-Receiving` ethRecipient

### Summary

The `streamEscrow::sendETHToTreasury` function transfers native ETH from the `streamEscrow` to `ethRecipient` using a low-level call. However, unlike the `NounsAuctionHouseV3::_safeTransferETHWithFallback` function, it does not handle the case where sending native ETH fails. This oversight could potentially cause auction settlements to fail and the system to be effectively "DDOSed" until the DAO selects a new compatible `ethRecipient`.

### Root Cause

`streamEscrow::sendETHToTreasury` function transfers ETH from `sctreamEscrow` to `ethRecipient` using a low-level call:
https://github.com/sherlock-audit/2024-11-nounsdao/blob/main/nouns-monorepo/packages/nouns-contracts/contracts/StreamEscrow.sol#L303-L309
```solidity
    //Sending ETH to treasury, but error is not handled, unlike NounsAuctionHouseV3
    function sendETHToTreasury(uint256 amount) internal {
        if (amount > 0) {
            (bool sent, ) = ethRecipient.call{ value: amount }('');
            //@audit what if we fail sending ether? just revert it!
            require(sent, 'failed to send eth');
            emit ETHStreamedToDAO(amount);
        }
    }
```
Unlike `NounsAuctionHouseV3::_safeTransferETHWithFallback`, where it wraps ETH as WETH in case of failure:
```solidity
    /**
     * @notice Transfer ETH. If the ETH transfer fails, wrap the ETH and try send it as WETH.
     */
    function _safeTransferETHWithFallback(address to, uint256 amount) internal {
        if (!_safeTransferETH(to, amount)) {
            IWETH(weth).deposit{ value: amount }();
            IERC20(weth).transfer(to, amount);
        }
    }
```
the code docs shows that thereâ€™s an expectation that the recipient `(to)` (`treasury`) might not accept native `ETH`(mabe because of future upgrades?). If the transfer fails, the contract will fall back to sending WETH. However, the `streamEscrow` does not handle this edge case, which can cause issues with auction settlements.
to see how this issue can DDOS auction settlement, lets take a look at `NounsAuctionHouseV3::_settleAuction`:
```solidity
        //if amount to stream is greater than 0, which happens if bid has an amount
        if (amountToStream > 0) {
            //then forward it to the stream escrow and create a new stream for the current noun which is auctioned
            streamEscrow.forwardAllAndCreateStream{ value: amountToStream }(_auction.nounId, streamLengthInTicks);
        } else {
            //if amount to stream is zero, that means either treasury took all the portion, or bid amount was zero, no one bought the nft
            //this forwards the ticks and sends etherPerTick to treasury
            streamEscrow.forwardAll();
        } 
```
Here, if `amountToStream` is non-zero, it calls `forwardAllAndCreateStream`; otherwise, it calls `forwardAll`. Both of these functions use `sendETHToTreasury` to transfer either `ethStreamedPerTick` or the remainder (from `createStream`) to the `ethRecipient` or treasury:
 `forwardAndCreateStream`:
```solidity
     function forwardAllAndCreateStream(uint256 nounId, uint16 streamLengthInTicks) external payable {
        forwardAll();

        createStream(nounId, streamLengthInTicks);
    }
```
`createStream`:
```solidity
        // the remainder is immediately streamed to the DAO
        uint256 remainder = msg.value % streamLengthInTicks;
        sendETHToTreasury(remainder);
```
`forwardAll`:
```solidity
        lastForwardTimestamp = toUint48(block.timestamp);

        sendETHToTreasury(ethStreamedPerTick);
```
This means if `ethRecipient` cannot accept native ETH, the entire settlement process will fail. The auction will be effectively "DDOSed" until a new ethRecipient is selected by the DAO.


### Internal pre-conditions

_No response_

### External pre-conditions

- `ethRecipient` not accepting native ether

### Attack Path

_No response_

### Impact

Auction settlements and creations will be effectively **DDOSed** until a new `ethRecipient` is set in the `streamEscrow`. Since the contract relies on successfully sending ETH to this address, any failure in this transfer will halt the entire settlement process, preventing the proper distribution of funds and completion of auctions. This could cause delays in processing bids, locking up auction funds, and requiring DAO intervention to resolve the issue by selecting a new recipient.

### PoC

_No response_

### Mitigation

To handle potential reverts at the ethRecipient in a manner similar to `NounsAuctionHouseV3::_safeTransferETHWithFallback`, you can modify the `sendETHToTreasury` function to first attempt the native ETH transfer and, if it fails, wrap the ETH as WETH and transfer that instead. Here's how the updated function would look:

```solidity
    //Sending ETH to treasury, but error is not handled, unlike NounsAuctionHouseV3
    function sendETHToTreasury(uint256 amount) internal {
        if (amount > 0) {
            (bool sent, ) = ethRecipient.call{ value: amount }('');
            if(!sent){
                  IWETH(weth).deposit{ value: amount }();
                  IERC20(weth).transfer(ethRecipient, amount);
            }
            emit ETHStreamedToDAO(amount);
        }
    }
```